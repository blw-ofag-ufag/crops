# ==============================================================================
#       title:  TABLE OF VALID AND DEPRECATED DIRECT PAYMENT CROPS
#        date:  2025-11-26
#      author:  Damian Oswald (damian.oswald@blw.admin.ch)
# description:  This dataset provides a complete inventory of AGIS crop codes
#               with German labels, including validity periods and a calculated
#               status flag identifying entries that are currently deprecated.
# ==============================================================================

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX schema: <http://schema.org/>
PREFIX : <https://agriculture.ld.admin.ch/crops/>
PREFIX ct: <https://agriculture.ld.admin.ch/crops/cultivationtype/>

SELECT
  (CONCAT(?name, " (", STR(?id), ")") AS ?crop)
  ?validFrom
  ?validTo
  ?deprecated
WHERE {
  ?cultivationtype :hasMembership ?membership .
  ?cultivationtype schema:name ?name .
  FILTER(LANG(?name) = "de")
  ?membership schema:identifier ?id .
  ?membership schema:name "AGIS" .
  OPTIONAL { ?membership schema:validFrom ?validFrom } .
  OPTIONAL { ?membership schema:validTo ?validTo } .
  BIND(
    !((!bound(?validFrom) || ?validFrom <= xsd:date(NOW())) &&
    (!bound(?validTo) || ?validTo > xsd:date(NOW()))) AS ?deprecated
  )
}
